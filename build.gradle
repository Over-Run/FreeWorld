plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11

idea.module.inheritOutputDirs = true

group 'org.overrun'
archivesBaseName = 'freeworld'
version game_version

def String lwjglVersion = '3.2.3'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
    for (String depend in ['', '-glfw', '-opengl']) {
        implementation "org.lwjgl:lwjgl$depend"
        for (String platform in ['windows', 'windows-x86', 'macos', 'linux', 'linux-arm64', 'linux-arm32']) {
            runtimeOnly "org.lwjgl:lwjgl$depend::natives-$platform"
        }
    }
    implementation 'io.github.over-run:glutils:0.2.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    implementation 'org.joml:joml:1.10.1'
    compileOnly 'org.jetbrains:annotations:20.1.0'
}

tasks.withType(JavaCompile) {
    options.encoding 'UTF-8'
}

jar {
    from 'LICENSE'
    manifest {
        attributes 'Main-Class': 'org.overrun.freeworld.Main',
                'Specification-Title': 'lwjgl3',
                'Specification-Vendor': 'LWJGL',
                'Specification-Version': lwjglVersion,
                'Implementation-Title': 'FreeWorld',
                'Implementation-Vendor': 'OverRun Organization',
                'Implementation-Version': archiveVersion,
                'Multi-Release': true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
    from 'LICENSE'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set 'javadoc'
    from 'build/docs/javadoc'
    from 'LICENSE'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(jar) {
                builtBy jar
            }
            artifact(sourcesJar) {
                builtBy sourcesJar
            }
            artifact(javadocJar) {
                builtBy javadocJar
            }
        }
    }
    repositories {
        mavenLocal()
    }
}
